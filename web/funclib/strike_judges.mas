<%args>
	$entry
	$by_round => undef
	$jpool	=> undef
</%args>
<%init>

	my $dbh = Tab::DBI->db_Main();

	my $judge_limit;

	if ($jpool) {
		$judge_limit = "
			and exists (
				select jpj.id
				from jpool_judge jpj
				where jpj.judge = judge.id
				and jpj.jpool = ".int($jpool). " ) ";
	} else {
		$judge_limit = " and judge.category = event.category "
	}

	my $sth = $dbh->prepare("
		select
			judge.id, judge.first, judge.middle, judge.last, judge.obligation, judge.hired,
			school.name, chapter.state,
			person.state, paradigm.id, person.id,
			strike.id, strike.conflict,
			school_strike.id, school_strike.conflict,
			free_strike.value,
			first_year.value,
			fyo_free_strikes.value,
			rtier_event.name,
			rtier_all.name,
			evstrike.id evstrike

		from (judge, entry, event, category, category judgecat)

			left join person on judge.person = person.id
			left join school on judge.school = school.id
			left join chapter on school.chapter = chapter.id
			left join person_setting paradigm on paradigm.person = person.id and paradigm.tag = 'paradigm'
			left join judge_setting free_strike on free_strike.judge = judge.id and free_strike.tag = 'free_strike'
			left join judge_setting first_year on first_year.judge = judge.id and first_year.tag = 'first_year'
			left join category_setting fyo_free_strikes on fyo_free_strikes.category = category.id and fyo_free_strikes.tag = 'fyo_free_strikes'

			left join rating_subset on rating_subset.id = event.rating_subset

			left join rating rating_event
				on rating_event.judge = judge.id
				and rating_event.type = 'coach'
				and rating_event.rating_subset = rating_subset.id

			left join rating_tier rtier_event
				FORCE INDEX FOR JOIN (PRIMARY)
				on rtier_event.id = rating_event.rating_tier

			left join rating rating_all
				on rating_all.judge = judge.id
				and rating_all.type = 'coach'
				and (rating_all.rating_subset = 0 or rating_all.rating_subset IS NULL)

			left join rating_tier rtier_all
				FORCE INDEX FOR JOIN (PRIMARY)
				on rtier_all.id = rating_all.rating_tier

			left join strike on strike.judge = judge.id
				and strike.registrant = 1
				and strike.entry = entry.id

			left join strike school_strike on school_strike.judge = judge.id
				and school_strike.registrant = 1
				and school_strike.school = entry.school

			left join strike evstrike
				on evstrike.judge = judge.id
				and evstrike.type = 'event'
				and evstrike.event = event.id

		where entry.id = ?
			and entry.event = event.id
			and event.category = category.id
			and category.tourn = judgecat.tourn
			and judge.category = judgecat.id
			and judge.active = 1
			and judge.school != entry.school

		$judge_limit
		order by judge.last
	");

	my %judges;
	my %meta;

	$sth->execute($entry);

	while (
		my (
			$judge_id, $judge_first, $judge_middle, $judge_last, $obligation, $hired,
			$school_name, $school_state,
			$person_state, $paradigm, $person_id,
			$strike, $conflict,
			$school_strike, $school_conflict,
			$free_strike,
			$first_year,
			$fyo_free,
			$rating_event,
			$rating_all,
			$evstrike
		) = $sth->fetchrow_array()
	) {

		next if $evstrike;

		$judges{$judge_id}{"first"}  = $judge_first;
		$judges{$judge_id}{"middle"} = $judge_middle;
		$judges{$judge_id}{"last"}   = $judge_last;
		$judges{$judge_id}{"person"} = $person_id;

		if ($paradigm) {
			$judges{$judge_id}{"paradigm"} = 1;
		}

		if ($school_name) {
			$judges{$judge_id}{"school_sort"} = $school_name;
			$judges{$judge_id}{"school"} = $school_name;
			$judges{$judge_id}{"state"} = $school_state;
		} else {
			$judges{$judge_id}{"school_sort"} = "0";
			$judges{$judge_id}{"school"} = "Hired ";
			$judges{$judge_id}{"state"} = $person_state;
		}

		if ($free_strike) {
			$judges{$judge_id}{"free_strike"} = 1;
			$meta{"freebie"} = 1;
		} elsif ($fyo_free && $first_year) {
			$judges{$judge_id}{"free_strike"} = 1;
			$meta{"freebie"} = 1;
		}

		if ($judges{$judge_id}{"free_strike"}) {
			$judges{$judge_id}{"count"} = 0;
		} elsif ($by_round) {
			$judges{$judge_id}{"count"} += $obligation;
			$judges{$judge_id}{"count"} += $hired;
		} else {
			$judges{$judge_id}{"count"} = 1;
		}

		if ($school_conflict) {

			$judges{$judge_id}{"conflict"} = "School Conflict";
			$judges{$judge_id}{"count"} = 0;

		} elsif ($conflict) {

			$judges{$judge_id}{"conflict"} = "Conflict";
			$judges{$judge_id}{"count"} = 0;

		} elsif ($school_strike) {

			$judges{$judge_id}{"conflict"} = "School Strike";
			$judges{$judge_id}{"count"} = 0;

		} elsif ($strike) {

			$judges{$judge_id}{"strike"} = $strike;
			$meta{"round_total"} += $obligation;
			$meta{"round_total"} += $hired;
			$meta{"judge_total"}++;

		} else {
			$meta{"round_total"} += $obligation;
			$meta{"round_total"} += $hired;
			$meta{"judge_total"}++;
		}

		$judges{$judge_id}{"round_total"} += $obligation;
		$judges{$judge_id}{"round_total"} += $hired;

		if ($rating_event) {
			$judges{$judge_id}{"rating"} = $rating_event;
			$meta{"ratings"} = 1;
		} elsif ($rating_all) {
			$judges{$judge_id}{"rating"} = $rating_all;
			$meta{"ratings"} = 1;
		}
	}

	$sth->finish();

	$sth = $dbh->prepare("
		select
			judge.id, shift.name

		from judge, shift, strike, category, event, entry
			where entry.id = ?
			and entry.event = event.id
			and event.tourn = category.tourn
			and judge.category = category.id
			and judge.active = 1
			and judge.id = strike.judge
			and strike.shift = shift.id
	");

	$sth->execute($entry);

	while (
		my (
			$judge_id, $shift_name
		) = $sth->fetchrow_array()
	) {
		next unless $judges{$judge_id};

		if ($judges{$judge_id}{"shifts"}) {
			$judges{$judge_id}{"shifts"} .= ' <br />';
		}

		$judges{$judge_id}{"shifts"} .= $shift_name;
		$meta{"shifts"}++;
	}

	$sth->finish();

	return \%judges, \%meta;

</%init>
